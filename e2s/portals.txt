@name Portals
@persist O:entity [Red, Blue]:entity
@persist [RedPos, BluePos]:vector

if(first()){
    runOnTick(1)
    runOnLast(1)
    
    O = owner()
}

function createBluePortal(){
    local R = O:eyeTrace()
    
    Blue:propDelete()
    Blue = seatSpawn("models/sprops/rectangles/size_4_5/rect_42x78x3.mdl", R:pos(), R:hitNormal():toAngle() + ang(90, 0, 0), 1)
    BluePos = R:pos()
    Blue:setAlpha(0)
    
    O:soundPlay(1, 0, "Weapon_Portalgun.fire_blue")
    
    holoCreate(1)
        holoModel(1, "cplane")
        holoMaterial(1, "models/effects/portalrift_sheet")
        holoColor(1, vec(0, 0, 255))
        holoDisableShading(1, 1)
        holoScaleUnits(1, Blue:boxSize() * 2)
        holoPos(1, Blue:pos() + (R:hitNormal()*2))
        holoAng(1, Blue:angles())
        holoParent(1, Blue)
}

function createRedPortal(){
    local R = O:eyeTrace()
    local Ang = R:hitNormal():toAngle() + ang(90, 0, 0)
    
    Red:propDelete()
    Red = seatSpawn("models/sprops/rectangles/size_4_5/rect_42x78x3.mdl", R:pos(), Ang, 1)
    RedPos = R:pos()
    Red:setAlpha(0)
    
    O:soundPlay(2, 0, "Weapon_Portalgun.fire_red")
    
    holoCreate(2)
        holoModel(2, "cplane")
        holoMaterial(2, "models/effects/portalrift_sheet")
        holoColor(2, vec(255, 0, 0))
        holoDisableShading(2, 1)
        holoScaleUnits(2, Red:boxSize() * 2)
        holoPos(2, Red:pos() + (R:hitNormal()*2))
        holoAng(2, Red:angles())
        holoParent(2, Red)
}

if(O:weapon():type() == "weapon_crowbar"){
    if(changed(O:keyAttack1()) & O:keyAttack1()){
        createBluePortal()
    }
    
    if(changed(O:keyAttack2()) & O:keyAttack2()){
        createRedPortal()
    }
}

if(Red:isValid() & Blue:isValid()){
    if(changed(Red:driver()) & Red:driver():isValid()){
        Red:ejectPod(BluePos + (Blue:up() * 50), Blue:up():toAngle())
        Blue:soundPlay(3, 0, "PortalPlayer.EnterPortal")
    }
    
    if(changed(Blue:driver()) & Blue:driver():isValid()){
        Blue:ejectPod(RedPos + (Red:up() * 50), Red:up():toAngle())
        Red:soundPlay(4, 0, "PortalPlayer.EnterPortal")
    }
}

if(last()){
    propDeleteAll()
}
